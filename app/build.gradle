apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'com.github.ben-manes.versions'

android {
    compileSdkVersion 27
    buildToolsVersion "28.0.2"
    dataBinding.enabled = true
    defaultConfig {
        applicationId "net.syncthing.lite"
        minSdkVersion 21
        targetSdkVersion 26
        versionCode 9
        versionName "0.2.1"
        multiDexEnabled true
    }
    sourceSets {
        main.java.srcDirs += "src/main/kotlin"
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    signingConfigs {
        release {
            storeFile = {
                def path = System.getenv("SYNCTHING_LITE_RELEASE_STORE_FILE")
                return (path) ? file(path) : null
            }()
            storePassword System.getenv("SIGNING_PASSWORD") ?: ""
            keyAlias System.getenv("SYNCTHING_LITE_RELEASE_KEY_ALIAS") ?: ""
            keyPassword System.getenv("SIGNING_PASSWORD") ?: ""
        }
    }
    buildTypes {
        debug {
            minifyEnabled false
        }
        release {
            signingConfig signingConfigs.release
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    packagingOptions {
        exclude 'META-INF/*'
    }
    dataBinding {
        enabled = true
    }
    lintOptions {
        // TODO: remove this as soon as possible
        abortOnError false
    }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation "org.jetbrains.anko:anko-commons:$anko_version"
    implementation "org.jetbrains.anko:anko-coroutines:$anko_version"
    implementation "com.android.support:design:$support_version"
    implementation "com.android.support:preference-v14:$support_version"

    /**
     * syncthing-java depends on the Apache HTTP Client
     * https://github.com/syncthing/syncthing-java/blob/dd020737ba5fc6a7c681a1d258025b8ddb2e8f67/core/build.gradle#L9
     *
     * Android itself contains an older version of this HTTP Client. Due to that, there is an
     * extra version of it which does not cause conflicts with the builtin client of Android.
     *
     * This extra implementation is included below. As this other version is used,
     * it's ignored as dependency of syncthing-java.
     */
    implementation ("com.github.Nutomic:syncthing-java:0.2.1") {
        exclude group: 'commons-logging', module:'commons-logging'
        exclude group: 'org.apache.httpcomponents', module:'httpclient'
        exclude group: 'org.slf4j'
        exclude group: 'ch.qos.logback'
    }
    implementation 'org.apache.httpcomponents:httpclient-android:4.3.5.1'

    implementation 'sk.baka.slf4j:slf4j-handroid:1.7.26'
    implementation 'com.google.zxing:android-integration:3.3.0'
    implementation 'com.google.zxing:core:3.3.0'
    implementation 'com.github.apl-devs:appintro:v4.2.3'

    implementation project(':androidrepository')
}
